buildscript {
	repositories {
		mavenLocal()
		maven {
			credentials {
				username nexusUser
				password nexusPassword
			}
			url "https://artifactory.prezi.com/plugins-snapshot"
		}
	}

	dependencies {
		classpath "com.prezi.boxfish:gradle-boxfish-plugin:1.0-SNAPSHOT"
	}
}

allprojects {
	group = "com.prezi.gradle.pride"
	apply plugin: "idea"
}

subprojects { subproject ->
	apply plugin: "maven"
	apply plugin: "prezi"
	apply plugin: "groovy"
	sourceCompatibility = 1.6
	targetCompatibility = 1.6

	description = "Pride is a tool to manage a pride of Gradle-built modules stored in different GitHub repositories"

	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url "http://gradle.artifactoryonline.com/gradle/libs"
		}
	}

	dependencies {
		compile "org.codehaus.groovy:groovy:1.8.6"
		testCompile "junit:junit:4.11"
		testCompile("org.spockframework:spock-core:0.7-groovy-1.8") {
			exclude group: "org.codehaus.groovy"
		}
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				pom.project {
					url "http://pride.github.com/"
					name subproject.name

					scm {
						url "scm:git@github.com:prezi/pride.git"
						connection "scm:git@github.com:prezi/prezi.git"
						developerConnection "scm:git@github.com:prezi/pride.git"
					}

					licenses {
						license {
							name "The Apache Software License, Version 2.0"
							url "http://www.apache.org/licenses/LICENSE-2.0.txt"
							distribution "repo"
						}
					}

					developers {
						developer {
							id "lptr"
							name "Lorant Pinter"
						}
					}
				}

				repository(url: "https://artifactory.prezi.com/plugins-release-local/") {
					authentication(userName: nexusUser, password: nexusPassword)
				}
				snapshotRepository(url: "https://artifactory.prezi.com/plugins-snapshot-local/") {
					authentication(userName: nexusUser, password: nexusPassword)
				}
			}
		}
	}

	task groovydocJar(type: Jar, dependsOn: groovydoc) {
		classifier = "javadoc"
		from "build/docs/groovydoc"
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = "sources"
	}

	artifacts {
		archives jar
		archives groovydocJar
		archives sourcesJar
	}
}

project("pride-core") {
	dependencies {
		compile "org.gradle:gradle-tooling-api:1.11"
	}

	processResources {
		filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
			version: version
		]
	}
}

project("pride") {
	apply plugin: "application"
	mainClassName = "com.prezi.gradle.pride.cli.PrideCli"

	dependencies {
		compile project(path: ":pride-core")
		compile "io.airlift:airline:0.6"
		runtime "org.slf4j:slf4j-simple:1.7.5"
	}

	configure(applicationDistribution) {
		from(rootProject.file("README.md")) {
			into "/"
		}
	}

	artifacts {
		archives(distZip) {
			classifier "dist"
		}
	}
}

project("gradle-pride-plugin") {
	dependencies {
		compile gradleApi()
		compile project(":pride-core")
	}
}
