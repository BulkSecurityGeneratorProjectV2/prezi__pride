buildscript {
	repositories {
		mavenLocal()
		maven {
			credentials {
				username nexusUser
				password nexusPassword
			}
			url "https://artifactory.prezi.com/plugins-snapshot"
		}
	}

	dependencies {
		classpath "com.prezi.boxfish:gradle-boxfish-plugin:1.0-SNAPSHOT"
	}
}

group = "com.prezi.gradle.pride"

apply plugin: "groovy"
apply plugin: "idea"
apply plugin: 'maven'
apply plugin: "prezi"
apply plugin: "application"

mainClassName = "com.prezi.gradle.pride.cli.PrideCli"

sourceCompatibility = 1.6
targetCompatibility = 1.6

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
        version: version
    ]
}

configure(applicationDistribution) {
	from(file("README.md")) {
		into "/"
	}
}

artifacts {
	archives(distZip) {
		classifier "dist"
	}
}

repositories {
	mavenLocal()
	maven preziDownloadRepo("external-libs")
}

dependencies {
	compile "org.codehaus.groovy:groovy:1.8.6"
	compile "io.airlift:airline:0.6"
	compile "org.gradle:gradle-tooling-api:1.11"
	testCompile 'junit:junit:4.11'
	runtime 'org.slf4j:slf4j-simple:1.7.5'
}

uploadArchives {
	repositories {
		repositories {
			mavenDeployer {
				repository(url: "https://artifactory.prezi.com/plugins-release-local/") {
					authentication(userName: nexusUser, password: nexusPassword)
				}
				snapshotRepository(url: "https://artifactory.prezi.com/plugins-snapshot-local/") {
					authentication(userName: nexusUser, password: nexusPassword)
				}
			}
		}
	}
}
