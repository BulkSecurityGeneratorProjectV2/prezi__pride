task testPrideInit { task ->
	def dir = copyTest(task, "01-pride-init")
	doLast {
		exec {
			workingDir file("$dir/git-module")
			commandLine "git", "init"
		}
		exec {
			workingDir file("$dir/git-module")
			commandLine "git", "config", "user.email", "test@example.com"
		}
		exec {
			workingDir file("$dir/git-module")
			commandLine "git", "config", "user.name", "Test User"
		}
		exec {
			workingDir file("$dir/git-module")
			commandLine "git", "add", "-A"
		}
		exec {
			workingDir file("$dir/git-module")
			commandLine "git", "commit", "--message", "Initial"
		}
		exec {
			workingDir file("$dir/git-module")
			commandLine "git", "remote", "add", "origin", "git@github.com:prezi/test"
		}
		pride(dir, "init", "--gradle-version", defaultGradleVersion)

		assert file("$dir/build.gradle").exists()
		assert file("$dir/gradlew").exists()
		assert file("$dir/gradlew.bat").exists()
		assert file("$dir/gradle").isDirectory()

		assert rawContents("$dir/settings.gradle", "//") == """
include 'file-module'
project(':file-module').projectDir = file('file-module')
include 'git-module'
project(':git-module').projectDir = file('git-module')
"""
		assert rawContents("$dir/.pride/version", "#") == """
0
"""
		assert asProps(file("$dir/.pride/config")) == asProps("""
gradle.version = ${defaultGradleVersion}
modules.0.name = file-module
modules.0.vcs = file
modules.1.name = git-module
modules.1.vcs = git
""")

		def exportFile = file("$temporaryDir/export.pride")
		pride(dir, "export", "--output", exportFile, "-O")

		assert asProps(exportFile) == asProps("""
gradle.version = ${defaultGradleVersion}
modules.0.name = file-module
modules.0.vcs = file
modules.1.name = git-module
modules.1.vcs = git
modules.0.remote = ${dir}/file-module
modules.0.revision = none
modules.1.remote = git@github.com:prezi/test
modules.1.revision = master
""")

		pride(dir, "remove", "file-module")
		assert rawContents("$dir/settings.gradle", "//") == """
include 'git-module'
project(':git-module').projectDir = file('git-module')
"""
		assert asProps(file("$dir/.pride/config")) == asProps("""
gradle.version = ${defaultGradleVersion}
modules.0.name = git-module
modules.0.vcs = git
""")
	}
}

task testTransitiveResolution { task ->
	def repoDir = file("$buildDir/repo")
	def prideDir = copyTest(task, "02-transitive-resolution/pride", [ repo: "file:///" + repoDir ])
	def moduleADir = file("$prideDir/module-a")
	def moduleBDir = copyTest(task, "02-transitive-resolution/module-b", [ repo: "file:///" + repoDir ])
	doLast {
		mkdir repoDir
		gradlew moduleADir, "uploadArchives"
		gradlew moduleBDir, "uploadArchives"
		pride prideDir, "init", "-v", "--gradle-version", defaultGradleVersion
		checkGradleOutput(prideDir, ["module-c:dependencies", "--configuration", "compile"]) { exitValue, output, error ->
			assert output.contains("""
compile - Compile classpath for source set 'main'.
\\--- com.prezi.example.transitive:module-b:1.0
     \\--- com.prezi.example.transitive:module-a:1.0 -> project :module-a

BUILD SUCCESSFUL
""")
		}

		checkGradleOutput(prideDir, ["module-c:checkDependencyVersions", "--configuration", "compile"]) { exitValue, output, error ->
			assert output.contains("""
Configuration "compile" in project ":module-c" requests version 1.0 of project ":module-a", but its current version (1.1) does not fulfill that request
""")
		}
	}
}

task testFailingModuleBuild { task ->
	def dir = copyTest(task, "03-failing-module-build")
	doLast {
		checkPrideOutput(dir, ["init"]) { exitValue, output, error ->
			assert exitValue != 0
			assert error.contains("""You can get more detailed information about the error by rerunning Pride with --verbose.""")
		}
		checkPrideOutput(dir, ["init", "--force", "--verbose"]) { exitValue, output, error ->
			assert exitValue != 0
			assert !error.contains("""You can get more detailed information about the error by rerunning Pride with --verbose.""")
			assert error.contains("""Caused by: java.lang.RuntimeException: Error during configuration time""")
		}
	}
}

tasks.findAll { it.name.startsWith("test") }.each {
	it.dependsOn clean
	it.dependsOn project(":pride-core").install
	it.dependsOn project(":gradle-pride-plugin").install
	it.dependsOn project(":gradle-pride-projectmodel-plugin").install
	it.dependsOn project(":pride").installDist
	check.dependsOn it
}

def pride(File dir, Object... arguments) {
	println "Running Pride with ${arguments.join(" ")}"
	exec {
		workingDir dir
		executable file("${project(':pride').projectDir}/build/install/pride/bin/pride")
		args arguments
	}
}

def checkPrideOutput(File dir, List<?> arguments, Closure check) {
	checkOutput(dir, file("${project(':pride').projectDir}/build/install/pride/bin/pride"), arguments, check)
}

def checkGradleOutput(File dir, List<?> args1, Closure check) {
	def arguments = new ArrayList<Object>(args1)
	arguments.add "--stacktrace"
	checkOutput(dir, file("$rootDir/gradlew"), args1, check)
}

def checkOutput(File dir, File command, List<?> arguments, Closure check) {
	def output = new ByteArrayOutputStream()
	def error = new ByteArrayOutputStream()
	println "Running ${command} with ${arguments.join(" ")} in ${dir}"
	def result = exec {
		workingDir = dir
		executable = command
		args = new ArrayList(arguments)
		standardOutput = output
		errorOutput = error
		ignoreExitValue = true
	}
	check(result.exitValue, new String(output.toByteArray()), new String(error.toByteArray()))
}

def gradlew(File dir, Object... args1) {
	def arguments = new ArrayList<Object>(Arrays.asList(args1))
	arguments.add "--stacktrace"
	arguments.add "--no-daemon"
	println "Running Gradle with ${arguments.join(" ")} in ${dir}"
	exec {
		workingDir dir
		executable "$rootDir/gradlew"
		args arguments.toArray()
	}
}

def copyTest(Task task, String name, Map filters = [:]) {
	File source = file("src/at/$name")
	File target = file("$buildDir/at/$name")
	task.doLast {
		delete target
		copy {
			from source
			into target
			filters.put("version", version)
			filter org.apache.tools.ant.filters.ReplaceTokens, tokens: filters
		}
	}
	return target
}

def rawContents(Object f, String comment) {
	"\n" + file(f).text.split("\n").findAll { !it.isEmpty() && !it.startsWith(comment) }.join("\n") + "\n"
}

def asProps(String contents) {
	def props = new Properties()
	props.load(new StringReader(contents))
	return props
}

def asProps(File f) {
	def props = new Properties()
	f.withInputStream {
		props.load(it)
	}
	return props
}
